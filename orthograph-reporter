#!/usr/bin/env perl
#--------------------------------------------------
# This file is part of Orthograph.
# Copyright 2012 Malte Petersen <mptrsen@uni-bonn.de>
# 
# Orthograph is free software: you can redistribute it and/or modify it under the
# terms of the GNU General Public License as published by the Free Software
# Foundation, either version 3 of the License, or (at your option) any later
# version.
# 
# Orthograph is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR
# A PARTICULAR PURPOSE. See the GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License along with
# Orthograph. If not, see http://www.gnu.org/licenses/.
#-------------------------------------------------- 

# Pragmas
use strict;         # make me write clean code
use warnings;       # cry if something seems odd
use autodie;        # die automatically on I/O functions

# Core modules
use Carp;           # carp and croak: warn and die with call stack
use Data::Dumper;   # great for debugging
use DBD::mysql;     # MySQL database driver
use DBI;            # database interface
use FindBin;        # locate the dir of this script during compile time
use Getopt::Long;   # command line options
use IO::File;       # object-oriented access to files
use List::Util qw( first min max sum );  # list functions
use Time::HiRes;    # high-resolution timer

# Custom modules
use lib $FindBin::Bin;                 # $Bin is the directory of the original script
use Orthograph::Config;                # configuration in global hashref $config
use Orthograph::Functions;             # functions for all Orthograph tools
use Wrapper::Mysql;
use IO::Tee;

my $config = $Orthograph::Config::config;	# copy config var

# MySQL settings
my $mysql_table_blast          = $config->{'mysql_table_blast'};
my $mysql_table_blastdbs       = $config->{'mysql_table_blastdbs'};
my $mysql_table_ests           = $config->{'mysql_table_ests'};
my $mysql_table_hmmsearch      = $config->{'mysql_table_hmmsearch'};
my $mysql_table_set_details    = $config->{'mysql_table_set_details'};
my $mysql_table_aaseqs         = $config->{'aaseqs'};
my $mysql_table_seqpairs       = $config->{'sequence_pairs'};
my $mysql_table_taxa           = $config->{'taxa'};
my $mysql_table_orthologs      = $config->{'orthologs'};

my $outdir                     = $config->{'output_directory'};
my $quiet                      = $config->{'quiet'};
my $verbose                    = $config->{'verbose'};

my $setname                    = $config->{'ortholog_set'};
my $speciesname                = $config->{'species_name'};
my $reference_taxa             = $config->{'reference_taxa'};

my $blast_evalue_threshold     = $config->{'blast_evalue_threshold'};
my $soft_threshold             = $config->{'soft_threshold'};
my $hmmsearch_evalue_threshold = $config->{'hmmsearch_evalue_threshold'};
my $hmmsearch_score_threshold  = $config->{'hmmsearch_score_threshold'};

my $outputdir                  = $config->{'output_directory'};
my $logdir                     = File::Spec->catdir($outputdir, 'log');

# mutually exclusive options
die "Fatal: Can't operate in both verbose and quiet mode\n"
	if ($verbose and $quiet);

# mutually exclusive options
die "Fatal: Can't use both e-value and score thresholds\n"
	if ($hmmsearch_evalue_threshold and $hmmsearch_score_threshold);

# More variables
my $bin_size             = 500;
my $count                = 0;
my $data_by_orthoid      = { };
my $out                  = { };
my $data_by_transcript   = { };
my $data_by_evalue       = { };
my $insert_n             = 1;
my $table                = [ ];
my $strict               = 0;
my $sort_by              = 'blasteval';
my @reftaxa              = ( );
my $species_id           = 0;
my $set_id               = 0;
my $match                = 0;
my $mismatches           = 0;
my $max_mismatches       = $soft_threshold;
my ($t0, $t1)            = 0;

# get the species id from the database
$species_id = Wrapper::Mysql::get_taxid_for_species($speciesname);
die "Fatal: No such species in database: '$speciesname'\n" unless ($species_id);

# get the set id from the database
$set_id = Wrapper::Mysql::get_set_id($setname);
die "Fatal: No such set in database: '$setname'\n" unless ($set_id);

# create view (a virtual table) for the log-evalues
Wrapper::Mysql::create_log_evalues_view($species_id)
	or die "Fatal: Could not create view: $!\n";

# Create the list of reference taxa
# This can be set in the config file, otherwise get all taxa in the set
if ($reference_taxa) { @reftaxa = split(/\s*,\s*/, $reference_taxa) }
else { @reftaxa = Wrapper::Mysql::get_taxa_in_set($set_id) }

# get a orthoid => list_of_aaseq_ids relationship from the db
my $aaseqs_in_orthoid = Wrapper::Mysql::get_orthologs_for_set_hashref($set_id);

# get a log-evalue->count relationship from the database
my $logevalue_count = Wrapper::Mysql::get_logevalue_count($species_id);
my @logevalues = sort { $a <=> $b } keys %$logevalue_count;


# classify the log-evalues into "bins" that hold a limited number of values
# this is done in order to speed up the database queries
my $logevalues_bins = &binify($bin_size, $logevalue_count);

# generate a table
my $transcripts_mapped_to = { };

# get the results in the form:
# evalue => {
#   orthoid => [
#     reciprocal_hit,
#     reciprocal_hit,
#     reciprocal_hit,
#     etc.
#   ]
# }
my $limit = 2000;
my $offset = 0;
my $from = -999;
my $to   = -600;
my $results;

# this will hold all transcripts that were found so we can check for redundancy
my @transcript_list;

# open log and table output files 
my $logfile = File::Spec->catfile($logdir, 'report.txt');
my $logfh = IO::File->new($logfile, 'w')
	or die "Fatal: Could not open report file $logfile\: $!\n";
my $tablefile = File::Spec->catfile($logdir, 'table.txt');

# setup IO::Tee
my $stdout = IO::Tee->new(
	\*STDOUT,
	$logfh,
);

# okay let's do the loop come on baby let's do the loop
BIN:
foreach my $bin (@$logevalues_bins) {

	$t0 = scalar time;
	# only one evalue
	if ($bin->{'min'} == $bin->{'max'}) {
		$results = Wrapper::Mysql::get_results_for_logevalue($set_id, $species_id, $bin->{'min'});
	}
	# evalue range
	else {
		$results = Wrapper::Mysql::get_results_for_logevalue_range($set_id, $species_id, $bin->{'min'}, $bin->{'max'});
	}
	$t1 = scalar time;
	if ($verbose) { printf $stdout "Fetched %d rows in %.2f seconds\n", scalar keys %$results, $t1 - $t0 }

	EVALUE:
	while (my ($logevalue, $result) = each %$results) {
		ORTHOID:
		while (my ($orthoid, $list) = each (%$result)) {
			# reset the mismatch counter
			$mismatches = 0;

			# has this orthoid been assigned before?
			# TODO don't skip, check whether the previous hit overlaps with this candidate
			#next ORTHOID unless exists $aaseqs_in_orthoid->{$orthoid};

			REHIT:
			# sort by reciprocal hit evalue
			foreach my $reciprocal_hit ( sort { $a->{'blast_evalue'} <=> $b->{'blast_evalue'} } @$list ) {
				# find out if the aaseq_id is one of those used for this orthoid
				#print $stdout "  checking out hit " , Dumper($reciprocal_hit) if $verbose;

				# has this aaseq been used in building this hmm and has this transcript
				# not been found previously? 
				# the function first from List::Util returns after the first occurence. 
				if ( first { /$reciprocal_hit->{'blast_hit'}/ } @{$aaseqs_in_orthoid->{$orthoid}}
					and not first {/$reciprocal_hit->{'hmmhit'}/} @transcript_list ) {

					$match = $reciprocal_hit;

					print $stdout "    $reciprocal_hit->{'blast_hit'} used in this HMM!\n"
						if $verbose;

					# one hit is enough if not in strict mode
					last REHIT unless $strict;
				}
				else {  # not found
					# cry
					++$mismatches;
					print $stdout "    aaseq $reciprocal_hit->{'blast_hit'} not used in this HMM (mismatch #", $mismatches, ") :(\n" if $verbose;

					# in a strict search, that's it for this orthoid
					next ORTHOID if $strict;

					# no match
					undef $match;

					# too many mismatches, we don't trust this one anymore
					if ($mismatches >= $max_mismatches) {
						print $stdout ">>    Too many mismatches, we don't trust this one anymore\n"
							if $verbose;
						next ORTHOID;
					}
				}

			# END RH
			}
			# so, was this a match?
			if ($match) {

				# yeah - here should be the part where the actual sequences are output
				# in fasta format
				printf $stdout ">>    trying to assign %s [%d:%d] to %s (%d with %.1e)\n",
					$$match{'hmmhit'},
					$$match{'start'},
					$$match{'end'},
					$orthoid,
					$$match{'blast_hit'},
					$$match{'blast_evalue'},
					unless $quiet;

				# this orthoid can't be found again
				# TODO don't delete yet... check whether transcripts overlap etc
				if (exists $$transcripts_mapped_to{$orthoid}) {
					if (&overlap($$transcripts_mapped_to{$orthoid}, $match)) {
						printf $stdout "discarding the new hit %s for %s\n", $$match{'hmmhit'}, $orthoid if $verbose;
						next ORTHOID;
					}
					else {
						push @{ $$transcripts_mapped_to{$orthoid} }, $match;
						#$$transcripts_mapped_to{$orthoid} = &insert($$transcripts_mapped_to{$orthoid}, $match);
						printf $stdout ">>    No overlap, added %s to %s\n", $$match{'hmmhit'}, $orthoid if $verbose;
						print "$orthoid so far: ", Dumper($$transcripts_mapped_to{$orthoid});
						next ORTHOID;
					}
				}

				# this transcript can't be found again either
				push @transcript_list, $$match{'hmmhit'};

				push @{$$transcripts_mapped_to{$orthoid}}, $match;

				# reset 
				undef $match;
			}
			else {
				# this orthoid has no match
				print $stdout "    $orthoid could not be assigned :((\n"
					unless $quiet;
			}

		# END ORTHOID
		} 

	# END EVALUE
	}
# END RESULT
}



my $tfh = IO::File->new($tablefile, 'w')
	or die "Fatal: Could not open table file $tablefile\: $!\n";
while (my ($o, $t) = each %$transcripts_mapped_to) {
	print $tfh $o;
	foreach my $hit (@$t) {
		printf $tfh "\t%s[%d:%d]", $$hit{'hmmhit'}, $$hit{'start'}, $$hit{'end'};
	}
	print $tfh "\n";
}
undef $tfh;

# yay
exit;


=head2 binify(BIN_SIZE, VALUE_COUNT)

binifies a value->count hashref, i.e. generates an arrayref of bins (order
based on the numerical order of the hash keys), each of which contain a hashref
with the min and max boundary of the bin:

  [
    {
      'min' => min(THIS_BIN),
      'max' => max(THIS_BIN)
    },
    { 
      'min' => min(THIS_BIN),
      'max' => max(THIS_BIN)
    }
  ]

=cut

sub binify {
	my $bin_size    = shift;
	my $value_count = shift;
	my $bins        = [ ];
	my $sum         = 0;
	my @bin         = ( );
	foreach my $value (sort {$a <=> $b} keys %$value_count) {
		# if adding this count would not exceed the bin size
		if ($sum + $value_count->{$value} < $bin_size) {
			push @bin, $value;
			$sum += $value_count->{$value};
		}
		# this is too large, start a new bin
		else {
			# if the bin has content
			if (scalar @bin) {
				# put min and max into @$bins
				push @$bins, { 'min' => min(@bin), 'max' => max(@bin) };
				# empty this bin
				@bin = ( );
				$sum = 0;
			}
			# put this count into the bin
			push @bin, $value;
			$sum += $value_count->{$value};
		}
	}
	# in case there is only one bin (happens when there are only few results), return that
	if (scalar @$bins == 0) { push @$bins, { 'min' => min(@bin), 'max' => max(@bin) } }
	return $bins;
}

sub overlap {
	my $already_matched = shift;
	my $candidate = shift;
	my $overlap   = 0;
	# the fragments do not overlap
	for (my $i = 0; $i < scalar @$already_matched; ++$i) {
		if ($$candidate{'end'} <= $$already_matched[$i]{'start'}) {
			printf $stdout "%s and %s do not overlap (%d-%d and %d-%d)\n", 
				$$already_matched[$i]{'hmmhit'},
				$$candidate{'hmmhit'},
				$$already_matched[$i]{'start'},
				$$already_matched[$i]{'end'},
				$$candidate{'start'},
				$$candidate{'end'}
					if $verbose;
			$overlap = 0;
		}
		# the fragments do not overlap
		elsif ($$candidate{'start'} >= $$already_matched[$i]{'end'}) { 
			printf $stdout "%s and %s do not overlap (%d-%d and %d-%d)\n", 
				$$already_matched[$i]{'hmmhit'},
				$$candidate{'hmmhit'},
				$$already_matched[$i]{'start'},
				$$already_matched[$i]{'end'},
				$$candidate{'start'},
				$$candidate{'end'}
					if $verbose;
			$overlap = 0;
		}
		# the fragments do overlap
		else {
			printf $stdout "%s and %s overlap (%d-%d and %d-%d)\n", 
				$$already_matched[$i]{'hmmhit'},
				$$candidate{'hmmhit'},
				$$already_matched[$i]{'start'},
				$$already_matched[$i]{'end'},
				$$candidate{'start'},
				$$candidate{'end'}
					if $verbose;
		 	$overlap = 1;
		}
	}
	if ($overlap == 0) { return 0 }
	return 1;
}

# this shall insert a new match at the correct position in the list of matches.
sub insert {
	my $already_matched = shift;
	my $new_match       = shift;
	my $insert          = 0;
	my $position        = 0;
	for (my $i = 0; $i < scalar @$already_matched; ++$i) {
		# this $already_matched lies before $new_match
		# TODO write a new class for the candidates!
		if ($already_matched->[$i]->{'end'} < $new_match->{'start'}) {
			# insert there
			printf "%s before %s, inserting\n", $new_match->{'hmmhit'}, $already_matched->[$i]->{'hmmhit'};
			$insert = 1;
			$position = $i - 1;
		}
		# $already_matched lies after $new_match
		elsif ($already_matched->[$i]->{'start'} > $new_match->{'end'}) {
			# insert here
			printf "%s after %s, inserting\n", $new_match->{'hmmhit'}, $already_matched->[$i]->{'hmmhit'};
			$insert = 1;
			$position = $i;
		}
		else {
			$insert = 0;
		}
	}
	if ($insert) {
		splice @{ $already_matched }, $position, 0, $new_match;
	}
	return $already_matched;
}

